plugins {
	id 'fabric-loom' version '1.7-SNAPSHOT'
	id 'maven-publish'
	id "com.modrinth.minotaur" version "2.+"
	id 'net.darkhax.curseforgegradle' version "1.1.16"
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	maven {url = 'https://maven.ladysnake.org/releases'}
	maven {
		url = "https://api.modrinth.com/maven"
		content {
			includeGroup "maven.modrinth"
		}
	}
	maven { url "https://jitpack.io/" }
	maven { url "https://cursemaven.com" }
	maven { url "https://maven.parchmentmc.org" }
	maven { url "https://maven.terraformersmc.com/" }
	maven { url "https://maven.shedaniel.me/" }
	mavenLocal()
}

dependencies {
	// Base
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.layered() {
		officialMojangMappings()
		// Use parchment mappings. NOTE: Parchment maven must be manually added. (https://maven.parchmentmc.org)
		parchment("org.parchmentmc.data:parchment-1.21:2024.07.28@zip")
	}
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modLocalRuntime"com.terraformersmc:modmenu:${project.mod_menu_version}"

	modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-base:${project.cca_version}"
	modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${project.cca_version}"

	// FTB
	modImplementation "curse.maven:ftb-quests-fabric-438496:5543247"
	modImplementation "curse.maven:ftb-library-fabric-438495:5583833"
	modImplementation "curse.maven:ftb-teams-fabric-438497:5448372"
	modLocalRuntime "curse.maven:ftb-xmod-compat-889915:5494733"
	modLocalRuntime "curse.maven:architectury-api-419699:5553799"
	modApi("teamreborn:energy:4.1.0") {
		exclude(group: "net.fabricmc.fabric-api")
	}

	// Thermoo
	modImplementation 'com.github.TheDeathlyCow:thermoo:v4.2'
	modLocalRuntime "curse.maven:scorchful-981400:5567705"
	modLocalRuntime "curse.maven:frostiful-715248:5560676"
	modLocalRuntime "curse.maven:thermoo-patches-1012677:5554110"
	modApi "org.ladysnake:satin:2.0.0"


	modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}") {
		exclude(group: "net.fabricmc.fabric-api")
	}


}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}
	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}

task publishCurseForge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
	apiToken = System.getenv("CF_TOKEN")
	//debugMode = true
	def mainFile = upload(1055023, remapJar)
	mainFile.changelog = new File("$project.rootDir/changelog.md").getText('UTF-8')
	mainFile.changelogType = "markdown"
	mainFile.releaseType = "release"
	mainFile.addGameVersion("1.21")
	mainFile.addModLoader("fabric")
	mainFile.displayName = project.archives_base_name + "-" + project.version
}